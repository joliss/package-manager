eof = { eoi } // report explicitly

newline = { "\r"? ~ "\n" }

non_control_char = { "\t" | '\u{0020}'..'\u{007E}' | '\u{00A0}'..'\u{10FFFF}' }
ws_char = { " " | "\t" }
ws = @{ ws_char+ }
// Like `ws?` but emits a pair even if empty
maybe_ws = @{ ws_char* }

// Trailing underscore because `comment` is a magic rule (as is `whitespace`)
comment_ = @{ "//" ~ non_control_char* }



manifest_eof = { manifest ~ eof }

manifest = {
    fields_not_newline_terminated
}

block = {
    "{" ~ maybe_ws ~ comment_? ~ newline
    ~ fields_newline_terminated
    ~ maybe_ws ~ "}"
}

fields_not_newline_terminated = {
    // Note that since blank_line matches "", this rule matches ""
    (field | blank_line)
    ~ (
        newline
        ~ (field | blank_line)
    )*
}

fields_newline_terminated = {
    (
        (field | blank_line) ~ newline
    )*
}

fields = {
    (field | blank_line)
    ~ (
        newline
        ~ (field | blank_line)
    )*
}

blank_line = @{ maybe_ws ~ comment_? }

version_constraint_component = {
    ("^" | ">=" | "<" | "")
    ~ version
}

version = @{
    '0'..'9'
    ~ ('0'..'9' | 'a'..'z' | 'A'..'Z' | "-" | ".")*
}


field = {
    maybe_ws
    ~ symbol
    ~ maybe_ws
    ~ arguments
    ~ comment_?
}

symbol = @{
    !("/")
    ~ ('a'..'z' | 'A'..'Z' | '0'..'9' | "_" | "-" | "/" )+
}

arguments = {
    positional_arguments
    ~ options
    ~ (block ~ maybe_ws)?
}

positional_arguments = {
    (positional_argument ~ maybe_ws)*
}

options = {
    (option ~ maybe_ws)*
}

option = {
    "TODO implement option"
}

positional_argument = {
    string
    | list
    | version_constraint_component
}

list_item = { string | list | block }

string = {
    "\""
    ~ (
        literal_character
        | escaped_quote
        | escaped_backslash
        | escaped_newline
        | escaped_tab
        | escaped_unicode
    )*
    ~ "\""
}

literal_character = {
    !("\\" | "\"")
    ~ non_control_char
}

escaped_quote = { "\\\"" }

escaped_backslash = { "\\\\" }

escaped_newline = { "\\n" }

escaped_tab = { "\\t" }

escaped_unicode = {
    "\\u{"
    ~ (
          (hex)
        | (hex ~ hex)
        | (hex ~ hex ~ hex)
        | (hex ~ hex ~ hex ~ hex)
        | (hex ~ hex ~ hex ~ hex ~ hex)
        | (hex ~ hex ~ hex ~ hex ~ hex ~ hex)
    )
    ~ "}"
}

hex = {
    '0'..'9' | 'a'..'f' | 'A'..'F'
}

whitespace_with_newline = {
    (ws_char | (comment_? ~ newline))
}

list = {
    "["
    ~ whitespace_with_newline*
    ~ (
        list_item
        ~ (
            whitespace_with_newline+
            ~ list_item
        )*
    )?
    ~ whitespace_with_newline*
    ~ "]"
}
